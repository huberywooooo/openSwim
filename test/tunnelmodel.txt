!!
!!!!-----------------------------swim--------------------------------------
!!!!
!       1.swim全称为seismic wave input methods，用于二维地震波斜入射传递矩阵；
!       2.地震波包括常见脉冲、天然波以及人造波的p、sv和sh波和扭转等六个分量；
!         输入信息node_inf.dat为节点数据，包括节点号，xyz坐标和影响面积；
!         分别命名为左，右，底；注意局部与整体坐标系的转换！
!       3.把隧道改成围岩，测试计算结果
!                                        by huberywoo, hbw8456@163.com
!                                        2023年7月,重庆三峡学院慎思楼426 
!!!!-----------------------------------------------------------------------
!!
finish
/clear
!!
!!!!--------------------前处理建模-------------------------------------
!!!!
!定义材料和几何参数（平面单元围岩衬砌、弹簧单元支座、梁单元初支、杆单元锚杆）
/prep7  
et,1,plane42,,,2
keyopt,1,3,2

et,2,14,,,2
et,3,3
keyopt,3,6,1
! et,4,1

xb=100
yb=100
depth=40

elastic=1e+8
miu=0.35
rho=2000
cohesion=1e+5
friction=20
shear=elastic/2/(1+miu)
lambda=elastic*miu/((1+miu)*(1-2*miu))
a=0.8
b=1.1
cp=sqrt((lambda+2*shear)/rho)
cs=sqrt(shear/rho)
kn=(lambda+2*shear)/(1+a)
kt=(shear)/(1+a)
cn=b*rho*cp
ct=b*rho*cs

e_size=5
dtime=0.02
tt=40
time_num=tt/dtime


!!!!--------------------单元材料-------------------------------------
!围岩
mp,dens,1,rho
mp,ex,1,elastic
mp,prxy,1,miu
tb,dp,1
tbdata,1,cohesion,friction


! 混凝土基本材料属性
mptemp,,,,,,,,  
mptemp,1,0  
mpdata,ex,2,,30e9   
mpdata,prxy,2,,0.25  
tb,miso,2,1,5,
tbtemp,0
tbpt,,0.0005,15e6
tbpt,,0.001,21e6
tbpt,,0.0015,24e6
tbpt,,0.002,24e6
tbpt,,0.003,24e6
tb,conc,2,1,9,  
tbtemp,0
tbdata,,0.5,0.9,2e6,20e6,,  
tbdata,,,,1,,,  
! 设定混凝土的裂缝张开剪力传递系数为0.5，
! 裂缝闭合剪力传递系数为0.9，
! 混凝土的单轴抗拉强度为3e6，单轴抗压强度为30e6，
! 开裂软化参数为1，其他空着使用默认值。
! 其参数具体意义参见《混凝土结构有限元分析》一书。

!初支
mp,ex,3,2.1e10
mp,prxy,3,0.25
mp,dens,3,2200

r,1,,,,,
r,2,(0.025*0.025)*3.14/4
r,3,0.20,0.20*0.20*0.20/12,0.20

!!!!--------------------隧道建模（参数修改）--------------------------------
*afun,deg   
!radial 
*set,r1,5.6 
*set,r2,1.72
*set,r3,16.36   
!angle  
*set,a1,90+18+11/60+39/3600 
*set,a2,54+55/60+13/3600
*set,a3,16+54/60+8/3600 
!thickness  
*set,t1,0.5 
*set,t2,3.5 
!model  
csys,1  
k,1,r1,90   
k,2,r1+t1,90
k,3,r1+t1+t2,90 
!o1 
k,101,0,0   
k,11,r1,90+a1   
k,12,r1+t1,90+a1
k,13,r1+t1+t2,90+a1 
!o2 
k,102,r1+t1-r2,90+a1
cskp,11,1,102,13,3,1,1, 
csys,11 
k,21,r2-t1,-a2  
k,22,r2,-a2 
k,23,r2+t2,-a2  

! wpcsys
csys,0
larc,1,11,101,r1
larc,2,12,101,r1+t1    
larc,3,13,101,r1+t1+t2

larc,11,21,102,r2-t1
larc,12,22,102,r2
larc,13,23,102,r2+t2
lsymm,x,all, , ,1000,0,0 
nummrg,all

larc,21,1021,101,r3-t1
larc,22,1022,101,r3
larc,23,1023,101,r3+t2
numcmp,all

k,101,4*r1,4*r1
k,102,4*r1,-4*r1
k,103,-4*r1,-4*r1
k,104,-4*r1,4*r1
k,105,0,4*r1

l,9,103
l,17,102
l,3,105
l,101,105
*do,i,1,4,1
l,100+i,101+i
*enddo


l,1,2$l,2,3$l,4,5$l,5,6$l,7,8$l,8,9
l,12,13$l,13,14$l,15,16$l,16,17

al,1,2,24,26
al,7,8,24,30
al,4,5,28,26
al,10,32,11,30
al,13,14,28,32
al,8,9,25,31
al,2,3,25,27
al,5,6,27,29
al,11,31,12,33
al,14,15,29,33

al,22,23,18,3,6,16
al,15,16,17,21
al,17,18,19,9,20,12
al,1,7,4,13,10

r,1,,,,,
r,2,(0.025*0.025)*3.14/4
r,3,0.23,0.23*0.23*0.23/12,0.23
csys,0
dsys,0


numcmp,all
k,101,xb/2,depth
k,102,xb/2,-yb+depth
k,103,-xb/2,-yb+depth
k,104,-xb/2,depth
k,105,-4*r1,depth
k,106,4*r1,depth
k,107,-4*r1,-yb+depth
k,108,4*r1,-yb+depth
k,109,xb/2,4*r1
k,110,xb/2,-4*r1
k,111,-xb/2,-4*r1
k,112,-xb/2,4*r1

a,104,105,21,112
a,21,112,111,20
a,111,20,107,103
a,19,20,107,108
a,19,108,102,110
a,18,19,110,109
a,18,109,101,106
a,106,105,21,22,18


! 修改梁单元方向，很重要
lsel,s,,,7,8,1
lsel,a,,,10,11,1
lreverse,all

allsel

csys,0
dsys,0
asel,all
agen,,all,,,xb/2,yb-depth,,,,1
kmodif,10,xb/2,yb-depth

lsel,s,,,1,3,1
lsel,a,,,7,9,1
lesize,all,,,16

lsel,s,,,4,6,1
lsel,a,,,10,12,1
lesize,all,,,4

lsel,s,,,24,32,2
lesize,all,,,2

lsel,s,,,25,33,2
lesize,all,,,5

lsel,s,,,13,15,1
lesize,all,,,15

lsel,s,,,16,18,1
lesize,all,,,6

lsel,s,,,19,23,4
lesize,all,,,7

lsel,s,,,20,22,2
lsel,a,,,38,48,10
lesize,all,e_size

lsel,a,,,21,43,22
lesize,all,e_size

lsel,s,,,34,36,2
lsel,a,,,39,41,2
lesize,all,e_size

lsel,s,,,45,51,2
lesize,all,e_size

lsel,s,,,53
lesize,all,,,14

lsel,s,,,35,37,2
lsel,a,,,50,52,2
lesize,all,e_size

lsel,s,,,40,46,2
lesize,all,e_size

allsel
type,1
mat,1
asel,s,,,11,13,2
amesh,all

type,1
mat,1
asel,s,,,22
amap,22,21,105,106,18   


allsel
asel,all
asel,u,,,11,13,2
asel,u,,,22
mshape,0,2d
mshkey,2
type,1
mat,1
amesh,all

lsel,s,,,2,14,3
type,3
real,3
mat,3
lmesh,all
esll,s
cm,chuzhi,elem


!定义组件，方便选择
asel,s,,,1,5,1
esla,s,1
cm,erchen,elem

asel,s,,,1,5,1
asel,a,,,14
esla,s,1
cm,kaiwa,elem

nsel,s,loc,x,0
cm,ndleft,node
esln
cm,elleft,elem

nsel,s,loc,x,xb
cm,ndright,node
esln
cm,elright,elem

nsel,s,loc,y,0
cm,ndbottom,node
esln
cm,elbottom,elem
allsel

!!!!------------------输出节点信息（按照3个边界）------------------------

!!!!---------弹簧和阻尼单元--
allsel
cmsel,s,ndleft
nsel,u,loc,y,0
*get,nodenum1,node,0,count
*get,nd1,node,0,num,min
*dim,nodes1,array,nodenum1
*dim,nxy1,array,nodenum1,2
*dim,node_area1,array,nodenum1,1
*get,node_max,node,,num,maxd
*get,real_max,rcon,,num,max  


*do,i,1,nodenum1,1
nodes1(i)=nd1
nxy1(i,1)=nx(nd1)
nxy1(i,2)=ny(nd1)
node_area1(i)=arnode(nd1)

node_max=node_max+1
real_max=real_max+1
n,node_max,nxy1(i,1)+0.1,nxy1(i,2)
type,2
r,real_max,kn/xb*node_area1(i),cn*node_area1(i)
real,real_max              !x轴normal弹簧阻尼
e,nodes1(i),node_max
d,node_max,,,,,,ux,uy

node_max=node_max+1
real_max=real_max+1
n,node_max,nxy1(i,1),nxy1(i,2)+0.1
type,2
r,real_max,kt/xb*node_area1(i),ct*node_area1(i)
real,real_max              !y轴tangential弹簧阻尼
e,nodes1(i),node_max
d,node_max,,,,,,ux,uy

nd1=ndnext(nd1)
*enddo
nodes1=
nxy1=
node_area1=

!!!!---------------------左侧弹簧----------------------------

allsel
cmsel,s,ndleft
*get,nodenum1,node,0,count
*get,nd1,node,0,num,min
*dim,nodes1,array,nodenum1
*dim,nxy1,array,nodenum1,2
*dim,node_area1,array,nodenum1,1
*get,node_max,node,,num,maxd

*do,i,1,nodenum1,1
nodes1(i)=nd1
nxy1(i,1)=nx(nd1)
nxy1(i,2)=ny(nd1)
node_area1(i)=arnode(nd1)
nd1=ndnext(nd1)
*enddo

*cfopen,nodeinf1,dat           !左边
*vwrite,nodes1(1),nxy1(1,1),nxy1(1,2),node_area1(1)
(4f12.4)
*cfclose

!!!!---------------------左侧荷载----------------------------


allsel
cmsel,s,ndright
nsel,u,loc,y,0
*get,nodenum2,node,0,count
*get,nd2,node,0,num,min
*dim,nodes2,array,nodenum2
*dim,nxy2,array,nodenum2,2
*dim,node_area2,array,nodenum2,1
*get,node_max,node,,num,maxd
*get,real_max,rcon,,num,max  

*do,i,1,nodenum2,1
nodes2(i)=nd2
nxy2(i,1)=nx(nd2)
nxy2(i,2)=ny(nd2)
node_area2(i)=arnode(nd2)

node_max=node_max+1
real_max=real_max+1
n,node_max,nxy2(i,1)+0.1,nxy2(i,2)
type,2
r,real_max,kn/xb*node_area2(i),cn*node_area2(i)
real,real_max              !x轴normal弹簧阻尼
e,nodes2(i),node_max
d,node_max,,,,,,ux,uy

node_max=node_max+1
real_max=real_max+1
n,node_max,nxy2(i,1),nxy2(i,2)+0.1
type,2
r,real_max,kt/xb*node_area2(i),ct*node_area2(i)
real,real_max             !y轴tangential弹簧阻尼
e,nodes2(i),node_max
d,node_max,,,,,,ux,uy

nd2=ndnext(nd2)
*enddo
nodes2=
nxy2=
node_area2=

!!!!---------------------右侧弹簧----------------------------

allsel
cmsel,s,ndright
*get,nodenum2,node,0,count
*get,nd2,node,0,num,min
*dim,nodes2,array,nodenum2
*dim,nxy2,array,nodenum2,2
*dim,node_area2,array,nodenum2,1
*get,node_max,node,,num,maxd

*do,i,1,nodenum2,1
nodes2(i)=nd2
nxy2(i,1)=nx(nd2)
nxy2(i,2)=ny(nd2)
node_area2(i)=arnode(nd2)
nd2=ndnext(nd2)
*enddo

*cfopen,nodeinf2,dat           !右边
*vwrite,nodes2(1),nxy2(1,1),nxy2(1,2),node_area2(1)
(4f12.4)
*cfclose

!!!!---------------------右侧荷载----------------------------
allsel
cmsel,s,ndbottom
*get,nodenum3,node,0,count
*get,nd3,node,0,num,min
*dim,nodes3,array,nodenum3
*dim,nxy3,array,nodenum3,2
*dim,node_area3,array,nodenum3,1
*get,node_max,node,,num,maxd
*get,real_max,rcon,,num,max  

*do,i,1,nodenum3,1
nodes3(i)=nd3
nxy3(i,1)=nx(nd3)
nxy3(i,2)=ny(nd3)
node_area3(i)=arnode(nd3)

node_max=node_max+1
real_max=real_max+1
n,node_max,nxy3(i,1),nxy3(i,2)+0.1
type,2
r,real_max,kn/yb*node_area3(i),cn*node_area3(i)
real,real_max              !y轴normal弹簧阻尼
e,nodes3(i),node_max
d,node_max,,,,,,ux,uy

node_max=node_max+1
real_max=real_max+1
n,node_max,nxy3(i,1)+0.1,nxy3(i,2)
type,2
r,real_max,kt/yb*node_area3(i),ct*node_area3(i)
real,real_max              !x轴tangential弹簧阻尼
e,nodes3(i),node_max
d,node_max,,,,,,ux,uy

nd3=ndnext(nd3)
*enddo

*cfopen,nodeinf3,dat           !底部
*vwrite,nodes3(1),nxy3(1,1),nxy3(1,2),node_area3(1)
(4f12.4)
*cfclose
!!!!---------------------底部弹簧与荷载----------------------------

allsel
esel,s,type,,2
cm,e_spring,elem
cmsel,s,ndleft
cmsel,a,ndright
cmsel,a,ndbottom
cm,n_fixed,node
cmsel,s,n_fixed
*get,node_num,node,0,count
*get,nd,node,0,num,min

allsel
eplot
save,'tunnel','db'